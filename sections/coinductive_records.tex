\chapter{Record Types by Observation in Idris}
\label{cha:coind-records-idris}
We have previously discussed the concept of data with a top-level product
structure. In this chapter we discuss how we have modified the current
implementation of the records in Idris to focus on observations rather
than constructions.

\section{Intuition}
To understand our implementation, let us first look at the current
implementation of records in Idris. In Figure~\ref{fig:records_in_idris} an
example of a product type, here a \texttt{Pair}, is defined as a
record. Syntactically, this looks a lot like a data declaration, except for the
\texttt{record} keyword. Also, we are required to define exactly one
constructor, unlike data declarations, where we can have zero or many.

\begin{figure}[h]
\begin{lstlisting}
record Pair : Type -> Type -> Type where
  MkPair : (fst : a) -> (snd : b) -> Pair a b
\end{lstlisting}
  \caption{Current record syntax in Idris.}
  \label{fig:records_in_idris}
\end{figure}

The difference between a record and a data declaration does not appear until just
before elaboration. Here the record is elaborated as a data declaration
(literally Figure~\ref{fig:records_in_idris}, with the keyword \texttt{data}
instead of \texttt{record}), and a set of functions generated. Both a
\emph{projection} and an \emph{update} function is generated for each named
parameter in the constructor, in the \texttt{Pair} example \texttt{fst} and
\texttt{snd}. These projection functions are then placed in a namespace with the
name of the type, in this case \texttt{Pair}. 

\begin{figure}[h]
\begin{lstlisting}
data Pair : Type -> Type -> Type where
  MkPair : (fst : a) -> (snd : b) -> Pair a b

namespace Pair
  fst : Pair a b -> a
  fst (MkPair x _) = x

  snd : Pair a b -> b
  snd (MkPair _ y) = y

  set_fst : Pair a b -> a -> Pair a b
  set_fst (MkPair _ y) x = MkPair x y

  set_snd : Pair a b -> b -> Pair a b
  set_snd (MkPair x _) y = MkPair x y
\end{lstlisting}
  \caption{\texttt{Pair} from Figure~\ref{fig:records_in_idris} desugared. Note
    that this is a slightly prettified version.}
  \label{fig:pair_desugared}
\end{figure}

An example of how records are desugared can be seen in
Figure~\ref{fig:pair_desugared}. This means that records are a high-level feature, syntactic sugar, and
as such so should our implementation of them be.

\subsection{Corecords}
Just as records are desugared to data declarations, corecords should be
desugared to codata declarations. From the parser, the difference between data and
codata declarations is a flag given to the elaborator. We can apply the same
approach to corecords, with a flag denoting whether the declaration should be
desugared to a data or codata declaration. 

\section{Implementation}
Our implementation is a syntactic desugaring. In the following we will describe
how each component of the new syntax translates to what in a desugared
version. So without further ado, we present the following record syntax: 

\begin{lstlisting}[mathescape]
record <name> <parameters> where
  <field$_1$ name> : <field$_1$ type>
  <field$_2$ name> : <field$_2$ type>
  ...
  <field$_n$ name> : <field$_n$ type>
  constructor <constructor name> <field or parameter names>
\end{lstlisting}

\paragraph{Parameters}
The parameters desugar to the type constructor. A parameter can be either (1) a
name bound to a type, e.g. \texttt{(name : Type)} or (2) a name by itself,
e.g. \texttt{a}. If a name not bound to a type is given it is assumed to be of type
\texttt{Type}. As such writing \texttt{a} is interpreted as \texttt{(a : Type)}

Be default, the parameters are explicit arguments to the type
constructor and implicit arguments to the data constructor. Both of these
defaults can be overridden. A parameter can be marked as implicit by
surrounding it with curly braces, replacing any existing parentheses. How we
stop parameters from being implicit to the data constructor will be discussed later.

\paragraph{Fields}
The fields defined by a list of names and types separated by a \texttt{:}. All
fields will, again by default, be explicit arguments to the data
constructor. Similar to the original Idris-record implementation, projection and
update functions are generated for each field.
\paragraph{Data Constructor}
The last line of the declaration is an, optional, customization of the data constructor. This
allows the user to define a custom name for the constructor, and change how its
arguments act.

\begin{lstlisting}
constructor <constructor name> <field or parameter names>
\end{lstlisting}

This, forgetting the \texttt{constructor} keyword, consists of two things, a
constructor name and a list of field and parameter names. Starting with the
constructor name, this allows the user to define a name for
the constructor if desired. If omitted a name
based on the type, avoiding name clashes, will be created during elaboration.

The last part, \texttt{field and parameter names}, is again optional, but can only be defined
if a custom constructor name is defined. Here,
the parameters and fields can be listed in order to change how they behave in the generated
data constructor. A name on its own will be forced to be explicit, and a
name surrounded by curly braces will be forced implicit. Names can be omitted,
leaving them untouched.

In the end all of this will be desugared to a data declaration with a
constructor from the parameters and fields to a record value.

\begin{figure}[h]
\begin{lstlisting}
record Pair a b where
  fst : a
  snd : b
  -- No custom constructor is defined. MkPair is inferred.

record Foo (param : Nat) where
  num : Int
  constructor MkFoo param 
  -- param is forced explicit 

record C {a} (n : Nat) where
  unhelpful : a
  xs : Vect n a
  constructor MkC {unhelpful} n
  -- unhelpful is forced implicit
  -- n is forced explicit

corecord Stream a where
  head : a
  tail : Stream a
  constructor (::) 
  -- Custom name (::) is defined for the constructor
\end{lstlisting}
  \caption{Record examples.}
  \label{fig:new_record_examples}
\end{figure}

\begin{figure}[h]
\begin{lstlisting}
data Pair : (a : Type) -> (b : Type) -> Type where
  MkPair : {a : Type} -> {b : Type} -> 
                (fst : a) -> (snd : b) -> Pair a b

data Foo : (param : Nat) -> Type where
  MkFoo : (param : Nat) -> (num : Int) -> Foo param

data C : {a : Type} -> (n : Nat) -> Type where
  MkC {a : Type} -> {unhelpful : a} -> 
           (n : Nat) -> (xs : Vect n a) -> C {a=a} n

codata Stream : (a : Type) -> Type where
  (::) : {a : Type} -> (head : a) -> (tail : Stream a) -> Stream a
\end{lstlisting}
  \caption{The (co)data declaration resulting from desugaring the (co)record
    declarations from Figure~\ref{fig:new_record_examples}}
  \label{fig:new_record_examples_desugared}
\end{figure}

In Figure~\ref{fig:new_record_examples} a few examples of the above described
syntax can be seen, and Figure~\ref{fig:new_record_examples_desugared} depicts how
they desugar. For the sake of simplicity only the desugared data and codata
declarations are shown, and the generated projection and update functions are omitted. More desugaring examples can be found in
Appendix~\ref{cha:record-examples}.

%%% Local Variables:
%%% mode: latex
%%% TeX-master: "../copatterns-thesis"
%%% End:
